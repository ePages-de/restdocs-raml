import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage

buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.9.RELEASE'
        classpath 'com.epages:restdocs-raml-gradle-plugin:0.2.11'
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.0'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'eclipse'
apply plugin: 'com.epages.restdocs-raml'
apply plugin: 'com.bmuschko.docker-remote-api'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.epages'


dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-hateoas'

    runtime 'com.h2database:h2'
    runtime 'org.atteo:evo-inflector:1.2.2'

    testCompile 'com.epages:restdocs-raml:0.2.11'
    testCompile 'org.json:json:20170516'
    testCompile 'com.jayway.jsonpath:json-path'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompile 'org.raml:raml-parser-2:1.0.15'
}

ramldoc {
    apiTitle = 'Notes'
    apiBaseUri = 'http://localhost:8080/'
    ramlVersion = "1.0"
}

//use mattjtodd/raml2html to generate html out of api.raml
task('pullRaml2Html', type:DockerPullImage, dependsOn: 'ramldoc') {
    description = 'Pulls docker pull mattjtodd/raml2html'
    repository = 'mattjtodd/raml2html'
    tag = '6.3.0'
}

task('createRaml2HtmlContainer', type:DockerCreateContainer, dependsOn: 'pullRaml2Html') {
    description = 'Creates container to generate HTML out of the RAML file'
    targetImageId { tasks.pullRaml2Html.repository + ':' + tasks.pullRaml2Html.tag }
    binds = ["${project.file('build/ramldoc').path}":'/raml']
    cmd = ['--rm', '-i', '/raml/api.raml', '-o', '/raml/api.raml.html']
}

task('raml2html', type:DockerStartContainer, dependsOn: 'createRaml2HtmlContainer') {
    description = 'Starts container to generate HTML out of the RAML file'
    targetContainerId { tasks.createRaml2HtmlContainer.containerId }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}
